#!/usr/bin/perl -w

#
# Generate flat documentation (without version numbers) from the standard
# documentation. Typical use (as user 'erlang'):
#
# cd www/erlang.org/public
# /home/otp/bin/otp_flatten_doc documentation/doc-5.5.5
#
# $Id: otp_flatten_doc,v 1.12 2018/05/22 12:11:46 otptest Exp $
#
use strict;
use File::Find;
use File::Basename;
use File::Copy;
use Cwd;

if (@ARGV == 0) {
    usage();
}

my $src = shift;
my $include_docsearch = 0;
my $docsearch_version = shift;
if ($docsearch_version > 23) {
    $include_docsearch = 1;
}

if (@ARGV != 0) {
    usage();
}

my $src_base = basename($src);
$src = getcwd() . "/$src" unless $src =~ m@^/@;
$src = File::Spec->canonpath($src);

usage() unless -d $src;
usage() unless $src_base =~ /^doc-([\d.]+)$/;
my $version = $1;

# Open log file
my $log_file = "/tmp/otp_flatten_doc.log";
open(FLATTEN_LOG, '>', $log_file);
print FLATTEN_LOG "Opened log file...\n";
print STDERR "Writing log to $log_file \n";

print STDERR "Examining directory $src...\n";
check_dir("$src/erts-$version");
check_dir("$src/lib");

flatten_doc($src);

my $app_dir;
my @modules;
my %module;
my %gifs;
my $dest_len;

close(FLATTEN_LOG);


sub flatten_doc {
    my($src) = @_;
    my $dest = getcwd() . "/doc";
    my $i = 1;
    
    while (-e "$dest-$i") {
	$i++;
    }
    $dest = "$dest-$i";
    mkdir $dest or die "$0: Failed to make directory $dest: $!\n";
    print STDERR "Writing flattened documentation to $dest...\n";
    $dest_len = length($dest);

    #
    # Scan the documentation to find the names of all html pages documenting a
    # module. (Actually, if there is an 'refpage' comment in the html file, we
    # we consider it to be a module.)
    #
    # Note, since there is currently two erlang.html (erts, kernel), we add
    # the erts version to the end.
    #
    find_module_doc("$src/lib");
    find_module_doc("$src/erts-$version");


    #
    # Copy the HTML pages for all modules into the 'man' sub directory.
    # Make a note of all GIF files referred from the modules.
    #

    my $module_dir = "$dest/man";
    mkdir $module_dir;

    foreach (@modules) {
	my ($base,$path,$suffix) = fileparse($_);
        my $app = $path;
        if ($app =~ /.*lib\/([^-]+).*/) {
            $app = $1;
        } elsif ($app =~ /.*erts-[0-9.]+\/doc/) {
            $app = "erts";
        }
        
	# application.html is ambiguous; partial workaround.
	$module{basename($base)} = $app;
    }

    foreach (@modules) {
	my $base = basename($_);
	copy_html($_, "$module_dir/$base");
    }

    #
    # Copy all GIF files that was referred from modules into the 'man'
    # directory.
    #

    foreach (keys %gifs) {
	copy($_, "$module_dir/" . basename($_));
    }

    #
    # Copy everything in the 'doc' directory to the top-level.
    #

    my $top_dir = "$dest";
    my $doc_src = "$src/doc";
    my $doc_src_len = length($doc_src);

    find(sub {
	return if length($File::Find::name) == $doc_src_len;
	my $rel = substr($File::Find::name, $doc_src_len+1);
	if (-d $_) {
	    mkdir "$top_dir/$rel";
	} elsif (/^otp-system-documentation-.*[.]pdf$/) {
	    copy_doc($_, "$top_dir/pdf/otp-system-documentation.pdf");
        } elsif ($include_docsearch && /^otp_doc[.]css$/) {
	    copy_html($_, "$top_dir/$rel");
            open FILE, ">>$top_dir/$rel";
            print FILE get_docsearch_css();
            close FILE;
	} else {
	    copy_html($_, "$top_dir/$rel");
	}
    }, $doc_src);

    #
    # Create an 'apps' directory and copy all applications to it.
    #
    $app_dir = "$dest/apps";
    mkdir $app_dir;

    foreach (<$src/lib/*>) {
        if (/erts/) { next; };
	copy_application($_);
    }
    my($erts) = <$src/erts-*>;
    copy_application($erts);

    #
    # Copy some stuff again to get references to application.html correct.
    #
    copy_html("$src/doc/man_index.html", "$dest/man_index.html");

}

sub find_module_doc {
    my $search_src = shift(@_);
    print FLATTEN_LOG "Searching for modules in $search_src \n";
    find(sub {
	return unless /\.html$/ and -f $_;
	open(FILE,$_) or die "ERROR: Can't open $File::Find::name: $!\n";
	my $line; 

	while (defined ($line = <FILE>)) {
	    if ($line =~ /<!-- refpage -->/) {
		close FILE;
		push @modules, $File::Find::name;
		return;
	    }
	}
	close FILE;
     }, $search_src);
}

sub copy_application {
    my($src_base) = @_;
    my $app = basename($src_base);
    $app =~ s/-.*$//;
    my $dir = "$app_dir/$app";
    $src_base .= "/doc";
    my $src = "$src_base/html";
    my $src_len = length($src);

    # Copy HTML
    mkdir $dir;
    find(sub {
	return if length($File::Find::name) == $src_len;
	my $rel = substr($File::Find::name, $src_len+1);
	if (-d $_) {
	    mkdir "$dir/$rel";
	} elsif (!$module{$_} || $module{$_} ne $app) {
	    copy_html($File::Find::name, "$dir/$rel");
	}
    }, $src);

    # Potential javadoc
    $src = "$src_base/javadoc";
    if (-d $src) {
	$src_len = length($src);
	$dir .= "/javadoc";
	mkdir $dir;
	find(sub {
	    return if length($File::Find::name) == $src_len;
	    my $rel = substr($File::Find::name, $src_len+1);
	    if (-d $_) {
		mkdir "$dir/$rel";
	    } elsif (!$module{$_} || $module{$_} ne $app) {
		copy_doc($File::Find::name, "$dir/$rel");
	    }
	}, $src);
    }

    # Copy PDF file.
    foreach my $pdf (<$src_base/pdf/*>) {
	my $new = basename($pdf);
	$new =~ s/-[\d.]+[.]pdf$/.pdf/;
	copy_doc($pdf, "$dir/$new");
    }
}

sub copy_html {
    my($src,$dest) = @_;

    if (-e $dest) {
	print STDERR "Warning! Replacing $dest with $src \n";
    } 

    if ($src !~ /\.html$/) {
	return copy_doc($src, $dest);
    }
    my $rel = substr($dest, $dest_len+1);

    my ($base,$src_app,$suffix) = fileparse($src);
    if ($src_app =~ /.*lib\/([^-]+).*/) {
        $src_app = $1;
    } elsif ($src_app =~ /.*erts-[0-9.]+\/doc/) {
        $src_app = "erts";
    }

    my $analytics = get_analytics();
    my $docsearch = get_docsearch_js();
    my $docsearch_html = get_docsearch_html();
    my $docsearch_expand = get_docsearch_expand();

    open SRC, $src or die "ERROR: Can't open $src: $!\n";
    open DEST, ">$dest" or die "ERROR: Can't open $dest for writing: $!\n";
    while (<SRC>) {
	s/(.)(<a href=)/$1\0$2/g;
	foreach (split "\0") {
	    if (m@<!-- Generated by javadoc@) {
		close SRC;
		close DEST;
		return copy_doc($src, $dest);
	    }
	    s@src="[^\"]*erlang-logo.png"@"src=\"" . path_to("erlang-logo.png", $rel) . '"' @e;
	    s@src="[^\"]*flipmenu[.]js"@"src=\"" . path_to("js/flipmenu/flipmenu.js", $rel) . '"' @e;
	    s@image[Pp]ath="[^\"]*flipmenu"@"imagePath=\"" . path_to("js/flipmenu", $rel) . '"' @e;
            s@src="[^\"]*erlresolvelinks[.]js"@"src=\"" . path_to("js/erlresolvelinks.js", $rel) . '"' @e;
            s@src="[^\"]*highlight[.]js"@"src=\"" . path_to("js/highlight.js", $rel) . '"' @e;
            s@__otpTopDocDir = '[^\']*doc/js/'@"__otpTopDocDir = '" . path_to("js/", $rel) . "'" @e;
	    if (m/href="([^\"]*)"/i) {
		my $href = $1;
		if ($href =~ /^javascript:erlhref\((.*)\);/) {
		    # my $url;
		    my(undef,$part,$mod) = split(/,\s*/, $1);
		    $mod =~ tr/\'//d;
		    $part =~ tr/\'//d;
		    my $mod_name = $mod;
		    $mod_name =~ s/#.*$//;
		    if ($part =~ m@doc/(.*)@) {
			$href = path_to($1, $rel) . "/$mod";
		    } elsif ($module{$mod_name}) {
			$href = path_to("man/$mod", $rel);
		    } else {
			$href = path_to("apps/$part/$mod", $rel);
		    }
		    $_ =~ s/href="[^\"]*"/href="$href"/i;
		} elsif ($href !~ /^https?:/ && $href !~ /^\#/ && $href !~ /^javascript:/) {
		    $href = normalize_link($href, $src);
		    my $base = $href;
		    my $anchor = '';
		    if ($base =~ s/(\#.*)$//) {
			$anchor = $1;
		    }
		    $base = basename($base);
		    if ($base eq 'otp_doc.css') {
			$href = path_to($base, $rel);
		    } elsif ($base =~ /^otp-system-documentation-.*[.]pdf$/) {
			$href = path_to("pdf/otp-system-documentation.pdf",
					$rel);
		    } elsif ($base =~ /[.]pdf$/) {
			my $app;
			($app = $base) =~ s/-[\d.]+[.]pdf$//;
			$href = path_to("apps/$app/$app.pdf", $rel);
		    } elsif ($href =~ m@^([.][.]/)+doc/(.*?.html)@) {
			$href = path_to($2, $rel);
		    } elsif ($href =~ m@^[.][.]/javadoc/(.*)@) {
			$href = path_to($1, $rel);
		    } elsif ($href =~ m@/([A-Za-z][^\-/]*)-[\d.]+/doc/html/index\.html@) {
			$href = path_to("apps/$1", $rel) . "/index.html";
		    } elsif ($module{$base} && $module{$base} eq $src_app) {
			$href = path_to("man/$base$anchor", $rel);
                    } elsif ($module{$base} && basename($dest) eq "man_index.html") {
			$href = path_to("man/$base$anchor", $rel);
		    } else {
			my $app = find_app($src);
			$href = path_to("apps/$app/$href", $rel)
			    if defined $app;
		    }
		    $_ =~ s/href="[^\"]*"/href="$href"/i;
		}
		print DEST $_;
	    } else {
		note_img_ref($src,$_);
		s@</body>@$analytics$&@;
                if ($include_docsearch) {
                    s@</body>@$docsearch$&@;
                    s@<ul class="expand-collapse-items">@$docsearch_html$&@;
                    s@<div class="search-expand[^"]*">@$&$docsearch_expand@;
                }
		print DEST $_;
	    }
	}
    }
    close DEST;
    close SRC;
}

sub normalize_link {
    my($link,$src) = @_;

    if ($link =~ m@^[.][.]/([^/]*)/(.*)@ && $1 eq basename(dirname($src))) {
	return $2;
    }
    $link;
}

sub find_app {
    my($dir) = @_;

    for (;;) {
	my $base = basename($dir);
	if ($base =~ /^([^\-]+)-[\d.]+$/) {
	    return $1;
	}
	$dir = dirname($dir);
	return if $dir eq '/' or $dir eq '.' or $dir eq $src;
    }
}
sub note_img_ref {
    my($src,$line) = @_;

    if ($line =~ /<IMG(.*?)>/) {
	$line = $1;
	if ($line =~ /SRC="([^\"]*)"/) {
	    my $gif = dirname($src) . "/$1";
	    $gifs{$gif} = 1;
	}
    }
}

sub path_to {
    my($to,$where) = @_;
    my $base = basename($to);
    return $base if dirname($to) eq dirname($where);
    my $up = up($where);
    "$up$to";
}

sub up {
    my($up) = @_;

    $up =~ s@[^/]+@..@g;
    $up .= "/";
    $up =~ s@^../@@;
    $up;
}

sub check_dir {
    my($name) = @_;
    -d $name or die "$0: Missing directory $name\n";
}

sub check_file {
    my($name) = @_;
    -f $name or die "$0: Missing file $name\n";
}

sub copy_doc {
    my($doc_src) = $_[0];
    my($doc_dest) = $_[1];
    print FLATTEN_LOG ("Copying $doc_src to $doc_dest\n");
    copy($doc_src, $doc_dest);
}

sub usage {
    die "usage: $0 doc-<version>\n";
}

sub get_analytics {
    return '';
#     <<'END'
# <script src="http://www.google-analytics.com/urchin.js" type="text/javascript">
# </script>
# <script type="text/javascript">
# _uacct = "UA-3045895-1";
# urchinTracker();
# </script> 
# END
}

sub get_docsearch_js {
    return <<END
<script type="text/javascript">
_docsearch_version = "$docsearch_version";
</script> 
<script src="/assets/js/doc-search.bundle.js"></script>
END
}

sub get_docsearch_html {
    return <<END
<div id="docsearch"></div>
<div style="padding-left: 1rem; padding-top: 0.1rem;"><a href="/doc/search?v=$docsearch_version">Paginated Search</a></div>
END
}

sub get_docsearch_expand {
    return <<'END'
        <button id="docsearch-mobile">
            <svg fill="#000000" xmlns="http://www.w3.org/2000/svg"  viewBox="0 0 50 50" width="24" height="24"><path d="M 21 3 C 11.621094 3 4 10.621094 4 20 C 4 29.378906 11.621094 37 21 37 C 24.710938 37 28.140625 35.804688 30.9375 33.78125 L 44.09375 46.90625 L 46.90625 44.09375 L 33.90625 31.0625 C 36.460938 28.085938 38 24.222656 38 20 C 38 10.621094 30.378906 3 21 3 Z M 21 5 C 29.296875 5 36 11.703125 36 20 C 36 28.296875 29.296875 35 21 35 C 12.703125 35 6 28.296875 6 20 C 6 11.703125 12.703125 5 21 5 Z"/></svg>
        </button>
END
}

sub get_docsearch_css {
    return <<'END'

@media screen and (max-width: 768px) {
    #docsearch {
        display: none;
    }
}

.DocSearch-Button {
    margin: 0 0 0 0px !important;
}

END
}

